apply plugin: 'me.champeau.mrjar'

multiRelease {
    targetVersions 8, 22
}

java22Test {
    jvmArgs += ["--enable-native-access=ALL-UNNAMED"]
}

configurations {
    java22Implementation.extendsFrom(implementation)
}

def noLibTestTask = tasks.register('noLibTest', Test) {
    description = 'Runs tests without lib'
    group = 'verification'

    classpath = files(jar.archiveFile.get().asFile) +
            configurations[sourceSets.java22.runtimeClasspathConfigurationName] +
            configurations[sourceSets.test.runtimeClasspathConfigurationName] +
            sourceSets.test.output
    testClassesDirs = sourceSets.test.output.classesDirs

    jvmArgs += ["--enable-native-access=ALL-UNNAMED"]
    systemProperty "java.library.path", ''
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(22)
    }

    dependsOn jar
}

tasks.named('check') {
    dependsOn(noLibTestTask)
}
