apply plugin: 'me.champeau.mrjar'

multiRelease {
    targetVersions 8, 19
}

compileJava19Java {
    options.compilerArgs += ["--enable-preview"]
}

compileJava19TestJava {
    options.compilerArgs += ["--enable-preview"]
}

java19Test {
    jvmArgs += ["--enable-preview", "--enable-native-access=ALL-UNNAMED"]
}

configurations {
    java19Implementation.extendsFrom(implementation)
}

def noLibTestTask = tasks.register('noLibTest', Test) {
    description = 'Runs tests without lib'
    group = 'verification'

    classpath = files(jar.archiveFile.get().asFile) +
            configurations[sourceSets.java19.runtimeClasspathConfigurationName] +
            configurations[sourceSets.test.runtimeClasspathConfigurationName] +
            sourceSets.test.output
    testClassesDirs = sourceSets.test.output.classesDirs

    jvmArgs += ["--enable-preview", "--enable-native-access=ALL-UNNAMED"]
    systemProperty "java.library.path", ''
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(19)
    }

    dependsOn jar
}

tasks.named('check') {
    dependsOn(noLibTestTask)
}
